---
eip: 721
title: ERC-721 非同质化(Non-Fungible)代币标准
author: William Entriken <github.com@phor.net>, Dieter Shirley <dete@axiomzen.co>, Jacob Evans <jacob@dekz.net>, Nastassia Sachs <nastassia.sachs@protonmail.com>
type: Standards Track
category: ERC
status: Final
created: 2018-01-24
requires: 165
---

## 简单概要

一个非同质化代币的标准接口，也被成为了契约(deeds,注: ERC-721曾经标题为Deed Standard)

## 摘要

这个标准定义了一种在智能合约中实现非同质化代币(NFTs)的标准API。这个标准提供了追踪或转移NFTs的基本功能。

我们考虑了用户自己拥有和交易NFTs或者委托给第三方经纪人/钱包/拍卖商("操作员")的情况。NFTs可以代表数字资产或实物资产的所有权。我们已经考虑了各种各样的资产，当然我们知道你会想到更多:

- 实物资产 — 房子，唯一的艺术品
- 虚拟收藏品 - 唯一的小猫图片，可收集的卡片
- "负值"资产 — 借贷，负债和其它责任

通常来说，所有的房子和谜恋猫都是不同的。NFTs是可区分的，因此你需要追踪每一个不同的个体的所有权。

## 目的

一个允许钱包/经纪人/拍卖商应用在以太坊上使用NFT的标准接口。我们提供简单的ERC-721智能合约以及能追踪*任意大*数量的NFTs的智能合约。另外还在下面讨论了一些额外应用。

本标准启发自ERC-20代币标准并基于两年的EIP-20的经验而创建。EIP-20不足以追踪NFTs因为每个资产都是不同的(非同质)然而代币之间却是等价的(同质)。

本标准与EIP-20的差别会在下面进行比较。

## 规范

本文中的关键词"MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY"和"OPTIONAL"和RFC 2119所描述的意思一致。

**所有符合ERC-721标准的合约都必须实现`ERC721`和`ERC165`的接口** (受下文"警告"的约束):

```solidity
pragma solidity ^0.4.20;

/// @title ERC-721 非同质代币标准
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-721.md
///  注意: 这个接口的ERC-165标识符是0x80ac58cd.
interface ERC721 /* is ERC165 */ {
    /// @dev 当任何NFT被任何机制修改了所有权时这个事件会发出
    ///  这个事件在NFTs被创建(`from` == 0)或者被销毁(`to` == 0)时也会发出
    ///  例外: 当合约创建的时候如果有NFTs同时被创建和分配不会发出此事件。
    ///  在转移的时候，该NFT的许可地址(如果设置了的话)会被重置为none。
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

    /// @dev 这个事件会在一个NFT的许可地址被修改或者重申的时候发出。
    ///  零地址意味着没有许可地址。
    ///  当一个Transfer事件被发出时，也同时代表着一个NFT的许可地址(如果设置了的话)
    ///  被设置为none。
    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

    /// @dev 当为所有者设置或撤销一个操作员时会发出此事件。
    ///  操作员具有替所有者管理所有的NFTs的权限。
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /// @notice 计算一个用户拥有的所有的NFTs的数量
    /// @dev 分配给零地址的NFTs会被认为无效，另外查询零地址的NFTs数量
    ///  会抛出异常
    /// @param _owner 被查询账户的地址
    /// @return `_owner`所拥有的NFTs的数量，可能为0
    function balanceOf(address _owner) external view returns (uint256);

    /// @notice 查看一个NFT的所有者
    /// @dev 分配给零地址的NFTs会被认为无效，另外查询零地址的NFTs的所有者
    ///  会抛出异常
    /// @param _tokenId 一个NFT的标识符
    /// @return 该NFT所有者的地址
    function ownerOf(uint256 _tokenId) external view returns (address);

    /// @notice 将一个NFT的所有权从一个地址转移到另一个地址。
    /// @dev 如果`msg.sender`不是当前所有者、被授权的操作员或者许可地址的话将会
    ///  抛出异常。如果`_from`不是当前所有者的话会抛出异常。
    ///  如果`_to`是零地址的话会抛出异常。
    ///  如果`_tokenId`不是一个有效NFT的话会抛出异常。当转移完成时，这个函数会
    ///  检查`_to`是不是一个智能合约(code size > 0)。如果是的话，它会调用`_to`
    ///  合约中的`onERC721Received`方法，且如果调用返回值不是
    ///  `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`会抛出异常。
    /// @param _from NFT的当前所有者
    /// @param _to 新所有者
    /// @param _tokenId 将被转移的NFT
    /// @param data 没有特定格式的附加数据，当调用时发送给`_to`
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

    /// @notice 将一个NFT的所有权从一个地址转移到另一个地址。
    /// @dev 这个方法的工作方式和另一个含有data参数的同名方法一样，差别在于
    ///  此方法将data设置为""。
    /// @param _from NFT的当前所有者
    /// @param _to 新所有者
    /// @param _tokenId 将被转移的NFT
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice 转移NFT的所有权 -- 调用者有责任确认
    ///  `_to`有能力接收NFTs否则可能会永久丢失该NFT
    /// @dev 如果`msg.sender`不是当前所有者、被授权的操作员或者许可地址的话将会
    ///  抛出异常。如果`_from`不是当前所有者的话会抛出异常。
    ///  如果`_to`是零地址的话会抛出异常。
    ///  如果`_tokenId`不是一个有效NFT的话会抛出异常。
    /// @param _from NFT的当前所有者
    /// @param _to 新所有者
    /// @param _tokenId 将被转移的NFT
    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice 修改或重申一个NFT的许可地址
    /// @dev 零地址意味着没有许可地址。
    ///  如果`msg.sender`不是当前NFT所有者或者被授权的操作员，则会抛出异常
    /// @param _approved 新的被授权的控制者
    /// @param _tokenId 所授权的NFT
    function approve(address _approved, uint256 _tokenId) external payable;

    /// @notice 设置或者撤销一个第三方("操作员")管理`msg.sender`所拥有的所有资产的权限
    /// @dev 发出ApprovalForAll事件。合约必须允许给一个所有者设置多个操作员。
    /// @param _operator 被授权的操作员的地址
    /// @param _approved 如果许可该操作员的话则返回True，否则如果是撤销许可则返回false
    function setApprovalForAll(address _operator, bool _approved) external;

    /// @notice 获取一个NFT的许可地址
    /// @dev 如果`_tokenId`不是一个有效的NFT则抛出异常
    /// @param _tokenId 需要查找许可地址的NFT
    /// @return 该NFT的许可地址，如果没有设置则是零地址
    function getApproved(uint256 _tokenId) external view returns (address);

    /// @notice 查询是否一个地址是另一个地址的授权操作员
    /// @param _owner NFTs所有者的地址
    /// @param _operator 被所有者授权的地址
    /// @return 如果`_operator`是`_owner`的授权操作员则返回True，否则返回false
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}

interface ERC165 {
    /// @notice 查询一个合约是否实现了某个接口
    /// @param interfaceID 接口的描述符，如ERC-165所述
    /// @dev 接口识别在ERC-165定义。这个函数耗费小于30,000 gas。
    /// @return 如果`interfaceID`不等于0xffffffff且合约实现了`interfaceID`
    ///  接口则返回`true`，否则返回`false`
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

一个钱包/经纪人/拍卖商应用如果想要支持安全转移，必须实现**钱包接口**。

```solidity
/// @dev 注意: 这个接口的ERC-165标识符是0x150b7a02。
interface ERC721TokenReceiver {
    /// @notice 注意处理NFT的接收
    /// @dev 在`transfer`调用后，ERC721智能合约会调用接收者合约中的这个函数
    ///  这个函数可能会抛出异常以拒绝转移并还原。
    ///  返回结果如果不是magic value都会导致交易被还原
    ///  注意: 消息发送者(sender)总是合约的地址。
    /// @param _operator 调用`safeTransferFrom`方法的调用者地址
    /// @param _from 该token的前所有者
    /// @param _tokenId 被转移的NFT的ID
    /// @param _data 没有特定格式的附加数据
    /// @return 除非抛异常，否则返回`bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
    function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
}
```

**元数据扩展**是实现ERC-721智能合约的可选项(看下方"警告")。这允许你的智能合约被询问它的名字以及你的NFTs所代表的资产的详细内容。

```solidity
/// @title ERC-721非同质化代币，可选的元数据扩展
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-721.md
///  注意: 这个接口的ERC-165标识符是0x5b5e139f.
interface ERC721Metadata /* is ERC721 */ {
    /// @notice 这个合约中一个NFTs集合的描述名称
    function name() external view returns (string _name);

    /// @notice 这个合约中的NFTs的缩写名称
    function symbol() external view returns (string _symbol);

    /// @notice 一个指定资产的唯一的统一资源标识符(URI)
    /// @dev 如果`_tokenId`不是NFT则抛出异常。URIs的定义遵从RFC 3986。
    ///  URI可能会指向一个符合"ERC721 Metadata JSON Schema"的JSON文件。
    function tokenURI(uint256 _tokenId) external view returns (string);
}
```

这是上述的"ERC721 Metadata JSON Schema"。

```json
{
    "title": "Asset Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this NFT represents"
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this NFT represents"
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this NFT represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
        }
    }
}
```

**枚举扩展**是ERC-721智能合约的可选项(看下方"警告")。它允许你的合约发布一个NFTs的完整列表并让它们公诸于众。

```solidity
/// @title ERC-721非同质化代币标准，可选的枚举扩展
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-721.md
///  Note: the ERC-165 identifier for this interface is 0x780e9d63.
interface ERC721Enumerable /* is ERC721 */ {
    /// @notice 计算这个合约追踪的NFTs的数量
    /// @return 这个合约追踪的有效NFTs的数量，它们每一个都有一个
    ///  可查询的所有者，且这个所有者不能是零地址
    function totalSupply() external view returns (uint256);

    /// @notice 查询一个有效的NFTs
    /// @dev 如果`_index` >= `totalSupply()`则抛出异常。
    /// @param _index 一个值小于`totalSupply()`的下标
    /// @return 下标为`_index`的NFT的ID,
    ///  (未指定排列顺序)
    function tokenByIndex(uint256 _index) external view returns (uint256);

    /// @notice 查询一个归属某个所有者的NFTs
    /// @dev 如果`_index` >= `balanceOf(_owner)`或者
    ///  `_owner`是零地址的话则抛出异常，意味着非有效NFTs。
    /// @param _owner 我们要查询的NFTs的所有者的地址
    /// @param _index 一个值小于`balanceOf(_owner)`的下标
    /// @return 所有者`_owner`拥有的下标为`_index`的NFT的ID
    ///   (未指定排列顺序)
    function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);
}
```

### 警告

0.4.20版本的Solidity接口语法不足以描述ERC-721标准。符合ERC-721的智能合约必须遵循下面的约定:

- Solidity issue #3412: 上面的接口中的每一个函数都包含显式的可变性约束。可变性约束从弱到强是: `payable`、implicit nonpayable、`view`、和 `pure`。你的实现必须最低满足这些接口中的可变性约束，但你可以使用更强的可变性约束。比如，合约中的一个`payable`的函数可以在实现时实现为nonpayble(没有指定状态可变性)。我们期望未来的Solidity版本会允许你更严格约束的合约继承这个接口，但对于0.4.20版本的一个变通方案是你可以在继承之前先修改这个接口为更严格的可变性约束。
- Solidity issue #3419: 合约如果实现了`ERC721Metadata` 或 `ERC721Enumerable`则应当也实现`ERC721`。ERC-721实现了ERC-165接口的需求。
- Solidity issue #2330: 如果一个函数在这个规范中是`external`的，那么如果合约使用`public`可见性也应该能够兼容。作为0.4.20版本的一个变通方案，你可以在继承之前先将这个接口修改为`public`约束。
- Solidity issues #3494, #3544: 使用`this.*.selector`被Solidity标记为warning，未来版本的Solidity将不标记它为错误。

*如果新版本的Solidity实现了警告中的某条内容，那么这个EIP就可能更新并删除那条警告，这样就和原始规范一致。*

## 基本原理

智能合约有很多推荐用途会需要追踪可区分的资产。已有或计划中的NFTs例子包括Decentraland中的土地，CryptoPunks里的同名朋克，以及类似DMarket或EnjinCoin这样的系统的游戏内道具。未来也会使用它来追踪实物资产，比如不动产(如同Ubitquity和Propy这样的公司所描绘的那样。在这些使用场景中的物件不应该"凑成一块"作为账本中的数字这一点很关键，而应该每个资产都有自己的所有权管理和被原子追踪的特性。不管这些资产是什么特点，如果我们有一个标准化的接口允许跨职能的资产管理和销售平台的话，能使这个生态更加强大。

**"NFT"单词选用**

调查中几乎所有人都对"NFT"这个词表示满意且广泛适用于各种各样的可区分数字资产。我们认为"deed"对这个标准的某些特定应用场景来说是合适的描述(特别是实物资产)。

*考虑过的替代方案: distinguishable asset, title, token, asset, equity, ticket*

**NFT描述符(ID)**

每一个NFT在ERC-721智能合约中都由一个唯一的`uint256`ID来标识。这个标识数字在合约的生命周期内都不应该被改变。`(contract address, uint256 tokenId)`可以全局唯一地标识以太坊链上的一个资产。尽管一些ERC-721智能合约可能会让ID从0开始逐个NFT递增会这样更简便，调用者却不应该假设特定的ID格式的存在，而必须将ID当作是一个"黑盒"。同时要注意一个NFTs可能会变成无效的(被销毁)。支持的枚举相关接口请查看枚举函数。

选择`uint256`类型能支持各种各样的应用的使用需求因为UUIDs和sha3哈希能直接转换为`uint256`。

**转移机制**

ERC-721标准化了一个安全转移函数`safeTransferFrom`(可带或不带一个类型`bytes`的参数)以及一个不安全的转移函数`transferFrom`。转移操作通常发起自:

- NFT的作者
- NFT的许可地址
- 当前所有者授权了可操作某个NFT的授权操作员

另外，一个授权的操作员可能会设置一个NFT的许可地址。这给钱包、经纪人和拍卖应用提供了强大的工具集来快速操作*大量*的NFTs.

转移和接收函数的文档仅仅说明了当交易必须抛出异常时的条件。你的实现可能在其它情况下抛出异常。这允许实现达成有趣的结果:

- **当合约中止时不允许转移** — 先前技术，谜恋猫部署的合约，第611行
- **指定地址为黑名单使其不允许接收NFTs** — 先前技术，谜恋猫部署的合约，第565、566行
- **不允许不安全的转移** — 除非`_to`等于`msg.sender`或`countOf(_to)`是非零值或之前是非零值，否则`transferFrom`抛出异常(因为这些情况下是安全的)
- **在双方的交易中收取费用** — 当使用非零`_approved`调用`approve`(之前是零地址)需要付款，当使用零地址调用`approve`时退款(之前是非零地址)，当调用任何转移函数时需要付款，邀请转移函数的参数`_to`等于`msg.sender`，要求转移函数的参数`_to`是NFT的许可地址
- **只读的NFT注册表** — 当调用`unsafeTransfer`、`transferFrom`、`approve`和`setApprovalForAll`时总抛出异常

失败的交易会抛出异常，这是在ERC-223、ERC-677、ERC-827和OpenZeppelin的SafeERC20.sol实现中的得到最佳实践。ERC-20定义了一个`allowance`特性，当调用之后再修改到另一个数量的时候会带来问题，如在OpenZeppelin issue \#438中所说的。在ERC-721，不会有allowance因为每个NFT都是唯一的，数量要么没有，要么是一。因此我们既使用了ERC-20原始设计中带来的好处，同时不会带来后来已经发现的一些问题。

创建NFTs("minting")以及销毁NFTs("burning")并没有在这个规范中给出。你的合约可能通过其它方式实现这俩功能。请查看`event`的文档浏览在创建和销毁NFTs时需要注意的问题。

我们质疑过`operator`对`onERC721Received`方法来说是否需要。在我们考虑的所有样例中，如果一个operator是重要的，那么他都可以转移该token给自己然后发送它 -- 然后他们就可以作为`from`参数的地址了。但这看起来不自然因为我们把操作员看作是暂时的代币所有者(转移代币给自己是多余的)。当操作员发送代币时，他是在代表自己执行操作，而却不是代表代币持有者执行操作。这就是为什么操作员和前一个代币所有者对代币接收者都很重要。

*考虑过的替代方案: 只允许ERC-20风格的两步式交易，要求转移函数永远不抛异常，要求所有函数返回一个布尔值表明操作是否成功。*

**ERC-165接口**

我们选择了标准接口检测(ERC-165)来暴露一个ERC-721智能合约所支持的接口。

一个未来的EIP可能会创建一个全局的合约接口注册器。我们完全支持这样的EIP且它将允许你的ERC-721实现来实现`ERC721Enumerable`、`ERC721Metadata`或其它代理给另外的合约的接口。

**Gas和复杂度** (考虑枚举扩展)

这个规范深思熟虑地实现了管理一些或*任意大*量的NFTs。如果你的应用的NFTs数量会变大，那么就应该避免在代码中使用for/while循环(看CryptoKitties bounty issue \#4)。因为这意味着你的合约可能会没办法伸缩然后gas费用会随着时间无限制增长。

我们已经部署了在测试网络上部署了一个XXXXERC721合约，它实例化并追踪了340282366920938463463374607431768211456个不同的非同质化代币(2^128)。这足以将每个IPV6地址分配给Ethereum帐户所有者, 或者追踪总尺寸是地球的一半那么多的几微米大小的纳米机器人的所有权。你可以在blockchain上查询它们。并且每个函数的gas消耗都小于查询ENS的消耗。

这个解释很直白: ERC-721标准是可伸缩的。

*考虑过的替代方案: 如果资产枚举函数需要一个for循环，那么最好删除它，改为在枚举函数中返回一个Solidity数组类型。*

**隐私性**

如目的章节所说，钱包/经纪人/拍卖商对确认NFTs的所有者有很强的需求。

考虑一个NFTs不可枚举的情况可能会很有趣，比如一个私密的资产所有权注册表，或一个部分私密的注册表。然而，私密无法实现，因为攻击者可以简单地(!)使用任何`tokenId`调用`ownerOf`函数就能轻易得到其所有者。

**Metadata Choices** (元数据扩展)

在元素句扩展中，我们需要加入`name`和`symbol`函数。我们审查过每一个代币EIP和草稿(ERC-20, ERC-223, ERC-677, ERC-777, ERC-827)都包含了这些函数。

我们提醒实现的作者，如果您反对使用这种机制，那么空字符串是`name`和`symbol`的一个有效的返回值。我们还提醒大家，任何智能合约都可以使用与*您的*合约相同的名称和符号。客户如何确定哪些ERC-721智能合约是知名的(规范的)则超出了本标准的范围。

我们还提供了一种将NFTs与URI关联的机制，预计许多实现将利用这一点为每个NFT提供元数据。推荐的图片大小来自Instagram，他们可能对图片的可用性更了解。URI可能是可变的(即它会不时地改变)。我们考虑用NFT表示房屋所有权，在这种情况下，关于房屋的元数据(图像、居住者等)可以随着自然改变。

元数据作为字符串值返回。 目前，这只能用于从“web3”调用，而不能从其他合约调用。 这是可以接受的，因为我们没有考虑过链上应用会查询此类信息的用例。

*考虑过的替代方案: 将每个资产的所有元数据放在区块链上(太贵)，使用URL模板查询元数据部分(URL模板不适用于所有URL方案，尤其是P2P URL)，multiaddr网络地址(不够成熟)*

**社区共识**

社区对ERC-721的原始问题进行了大量的讨论，另外我们还在Gitter上举行了直播会议，大家对此问题做了很好的陈述以及广告(在Reddit，Gitter的#ERC频道和原始的ERC-721问题)。感谢所有的参与者:

- [@ImAllInNow](https://github.com/imallinnow) Rob from DEC Gaming / Presenting Michigan Ethereum Meetup Feb 7
- [@Arachnid](https://github.com/arachnid) Nick Johnson
- [@jadhavajay](https://github.com/jadhavajay) Ajay Jadhav from AyanWorks
- [@superphly](https://github.com/superphly) Cody Marx Bailey - XRAM Capital / Sharing at hackathon Jan 20 / UN Future of Finance Hackathon.
- [@fulldecent](https://github.com/fulldecent) William Entriken

第二个事件是在ETHDenver 2018会议举行的关于可区分资产标准的讨论(相关笔记已发表)。

我们在这个过程中非常包容，并邀请任何有疑问或贡献的人参与我们的讨论。但是，编写的这个标准仅用于支持此处列出的已知用例。

## 向后兼容

我们采用了来自ERC-20规范的语法如`balanceOf`、`totalSupply`、`name`和`symbol`。某些实现可能包含一个返回`uint8(0)`的`decimals`函数如果它在支持本标准的同时想要更兼容ERC-20，但我们发现如果所有的ERC-721都要实现`decimals`函数的话太勉强了。

截止到2018年2月的NFT实现例子:

- CryptoKitties -- 和这个标准的一个早期版本兼容。
- CryptoPunks -- 和ERC-20部分兼容，但不是很容易泛化因为其合约中直接包含了拍卖函数并且显式地使用将资产叫做"punks"地函数名。
- Auctionhouse Asset Interface -- 作者需要一个用于Auctionhouse ÐApp的通用接口(目前是个冰盒)。他的"资产"合约非常简单，但缺少ERC-20兼容性、`approve()`功能，以及元数据功能。这些成果在EIP-173的讨论中被引用。

注意: 如Curio Cards和Rare Pepe这样的"限量版、可收藏的代币"*不是*可区分资产。它们实际上是个体同质的代币集合，每一个都是由各自的智能合约来追踪并且有各自的供应总量(在极限情况下可能是`1`).

`onERC721Received`函数明确地作为可能在特定情况下无意中返回1 (`true`)的旧的部署合约的变通方案，即使他们没有实现一个函数(看Solidity的DelegateCallReturnValue bug)。通过返回和检查一个magic value，我们可以将真实的肯定返回值和空洞的`true`返回值区分开来。

## 测试用例

0xcert ERC-721代币包含了使用Truffle编写的测试用例。

## 实现

0xcert ERC721 -- 一个引用实现

- MIT licensed，因此你可以在你的项目中免费使用它
- 包含测试用例
- 找bug有奖，如果你发现错误可以获得奖赏

Su Squares -- 一个供你租用空间放图片的广告平台

- 寻找此标准和实现的问题来获得Su Squares Bug奖金
- 实现了完整的标准和所有可用的接口

ERC721ExampleDeed -- 一个样例实现

- 使用了OpenZeppelin项目的格式来实现

XXXXERC721, by William Entriken -- 一个可扩展的样例实现

- 在测试网络部署了10亿级的资产，同时支持元数据扩展的所有查找方式。这展示了良好的伸缩性。

## 引用

**标准**

1. ERC-20 Token Standard. https://github.com/ethereum/EIPs/blob/master/EIPS/eip-20.md
1. ERC-165 Standard Interface Detection. https://github.com/ethereum/EIPs/blob/master/EIPS/eip-165.md
1. ERC-173 Owned Standard. https://github.com/ethereum/EIPs/issues/173
1. ERC-223 Token Standard. https://github.com/ethereum/EIPs/issues/223
1. ERC-677 `transferAndCall` Token Standard. https://github.com/ethereum/EIPs/issues/677
1. ERC-827 Token Standard. https://github.com/ethereum/EIPs/issues/827
1. Ethereum Name Service (ENS). https://ens.domains
1. Instagram -- What's the Image Resolution? https://help.instagram.com/1631821640426723
1. JSON Schema. http://json-schema.org/
1. Multiaddr. https://github.com/multiformats/multiaddr
1. RFC 2119 Key words for use in RFCs to Indicate Requirement Levels. https://www.ietf.org/rfc/rfc2119.txt

**问题**

1. 最初的ERC-721问题. https://github.com/ethereum/eips/issues/721
1. Solidity Issue \#2330 -- 接口函数应该是external的。 https://github.com/ethereum/solidity/issues/2330
1. Solidity Issue \#3412 -- 实现接口：允许更严格的可变性约束。 https://github.com/ethereum/solidity/issues/3412
1. Solidity Issue \#3419 -- 接口无法继承。 https://github.com/ethereum/solidity/issues/3419
1. Solidity Issue \#3494 -- 编译器错误地判定`selector`函数。 https://github.com/ethereum/solidity/issues/3494
1. Solidity Issue \#3544 -- 无法拿到`transfer`函数的Selector。 https://github.com/ethereum/solidity/issues/3544
1. CryptoKitties Bounty Issue \#4 -- 列出一个用户的所有猫咪的算法的复杂度高达`O(n^2)`. https://github.com/axiomzen/cryptokitties-bounty/issues/4
1. OpenZeppelin Issue \#438 -- `approve`方法的实现违反了ERC20标准。 https://github.com/OpenZeppelin/zeppelin-solidity/issues/438
1. Solidity DelegateCallReturnValue Bug. http://solidity.readthedocs.io/en/develop/bugs.html#DelegateCallReturnValue

**讨论**

1. Reddit (第一次直播讨论公告). https://www.reddit.com/r/ethereum/comments/7r2ena/friday_119_live_discussion_on_erc_nonfungible/
1. Gitter #EIPs (第一次直播讨论公告). https://gitter.im/ethereum/EIPs?at=5a5f823fb48e8c3566f0a5e7
1. ERC-721 (第一次直播讨论公告). https://github.com/ethereum/eips/issues/721#issuecomment-358369377
1. ETHDenver 2018. https://ethdenver.com

**NFT实现和其它项目**

1. CryptoKitties. https://www.cryptokitties.co
1. 0xcert ERC-721 Token. https://github.com/0xcert/ethereum-erc721
1. Su Squares. https://tenthousandsu.com
1. Decentraland. https://decentraland.org
1. CryptoPunks. https://www.larvalabs.com/cryptopunks
1. DMarket. https://www.dmarket.io
1. Enjin Coin. https://enjincoin.io
1. Ubitquity. https://www.ubitquity.io
1. Propy. https://tokensale.propy.com
1. CryptoKitties Deployed Contract. https://etherscan.io/address/0x06012c8cf97bead5deae237070f9587f8e7a266d#code
1. Su Squares Bug Bounty Program. https://github.com/fulldecent/su-squares-bounty
1. XXXXERC721. https://github.com/fulldecent/erc721-example
1. ERC721ExampleDeed. https://github.com/nastassiasachs/ERC721ExampleDeed
1. Curio Cards. https://mycuriocards.com
1. Rare Pepe. https://rarepepewallet.com
1. Auctionhouse Asset Interface. https://github.com/dob/auctionhouse/blob/master/contracts/Asset.sol
1. OpenZeppelin SafeERC20.sol Implementation. https://github.com/OpenZeppelin/zeppelin-solidity/blob/master/contracts/token/ERC20/SafeERC20.sol

## 版权

版权以及相关权利由此[CC0](https://creativecommons.org/publicdomain/zero/1.0/)声明弃权。
